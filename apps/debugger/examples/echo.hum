#
# Demonstrate Console I/O Device (stdio)
#

DEF is_number(n) AS not(eq(neg(n), ?))

# A write request looks like (to_cancel callback fixnum),
# where to_cancel is the optional customer for a cancel capability,
# and callback is the customer that will receive the result.
# The result looks like (#unit) on success, and (#? . error) on failure.

DEF strout_beh(cust, out, str) AS \(rv, err).[
    CASE str OF
    (first, rest) : [
        SEND (?, SELF, first, NIL) TO out       # output character
        BECOME strout_beh(cust, out, rest)
    ]
    _ : [ SEND (rv, err) TO cust ]
    END
]
CREATE say_hello WITH strout_beh(println, stdio,
    'H', 'e', 'l', 'l', 'o', '\n', NIL)
SEND (?, NIL) TO say_hello

# A read request looks like (to_cancel callback),
# where to_cancel is the optional customer for a cancel capability,
# and callback is the customer that will receive the result.
# The result looks like (fixnum) on success, and (#? . error) on failure.

DEF echo_beh(io) AS \(rv, err).[
    CASE is_number(rv) OF
    TRUE : [ SEND (?, SELF, rv, NIL) TO io ]    # output character
    _ : [ SEND (?, SELF, NIL) TO io ]           # input character
    END
]
CREATE echo WITH echo_beh(stdio)
SEND (?, echo, NIL) TO stdio                    # input character

# Documentation for the standard i/o device can be found at
# https://github.com/organix/uFork/blob/main/docs/io_dev.md
